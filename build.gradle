plugins {
    id 'checkstyle'
}
apply plugin: 'idea'
//and standard one
apply plugin: 'java'
apply plugin: 'application'

checkstyle {
    toolVersion = '8.41';
}

checkstyleMain.exclude('**/org/netbeans/editor/**')

repositories {
    mavenCentral()
}

logger.lifecycle('Java version '+JavaVersion.current())

sourceSets {
    main {
        java {
            srcDirs += ['src/main']
        }
        resources {
            srcDir 'src/main'
            include 'studio*.properties'
            include 'org/netbeans/editor/Bundle*.properties'
            include 'images/**'
        }
    }
    test {
        java.srcDirs += ['src/test']
    }

    intTest {
        java.srcDirs += ['src/test-integration']
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {
    implementation group: 'org.netbeans.api', name: 'org-openide-awt', version: 'RELEASE113'
    implementation group: 'org.jfree', name: 'jfreechart', version: '1.0.14'
    implementation group: 'org.apache.poi', name: 'poi', version: '4.1.2'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.2'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.11.2'
    testImplementation group: 'org.assertj', name: 'assertj-swing-junit', version:'3.9.2'
    intTestImplementation group: 'org.assertj', name: 'assertj-swing-junit', version:'3.9.2'
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRunimeOnly.extendsFrom runtimeOnly
}

test {
    useJUnit()
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
}

application {
    mainClassName = 'studio.core.Studio'
}

jar {
    manifest {
        attributes 'Main-Class': 'studio.core.Studio'
    }
}
